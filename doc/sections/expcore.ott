% Formalization of Explicit CoC

embed {{ tex-preamble
  \newcommand{\labeledjudge}[1]{\vdash_{\!\!\mathsf{#1} } }
  \newcommand{\kw}[1]{\mathsf{#1} }
}}

%%% Metavariables
metavar x, y, z, d, X, D, K, N, R, th {{ tex \theta }}, aa {{ tex \alpha }}, bb {{ tex b }}, nn {{ tex n }} ::= {{ com Variable names }}
indexvar i, j, n ::= {{ com Index variables }}

%%% Syntax
grammar

%% Expressions
e, t {{ tex \tau }}, T {{ tex \sigma }} :: 'e_' ::=       {{ com Expressions    }}
  | x                         ::  :: Var        {{ com Variable }}
  | star                      ::  :: Star       {{ com Type of types }}
  | e1 e2                     ::  :: App        {{ com Application }}
  | \ x : t . e               ::  :: Lam        {{ com Abstraction }}
  | Pi x : t  . T             ::  :: Pi         {{ com Product }}
  | castup [ t ] e            ::  :: CastUp     {{ com Cast up by expansion}}
  | castdown e                ::  :: CastDown   {{ com Cast down by reduction }}
  | mu x : t . e              ::  :: Mu         {{ com General recursion }}
  | e1 [ x |-> e2 ]           :: M:: Sub        {{ com Substitution }}
  | ( e )                     :: M:: Parens     {{ com Parenthesized expression }}
  | let x : t = e2 in e1      :: M:: LetBinding {{ com Let binding }}
  | t1 -> t2                  :: M:: FuncType   {{ com Function type }}
  | lit                       :: M:: Literals   {{ com Literals }}
  | e *                       :: M:: Starred    {{ com Starred expression }}
  | << e >>                   :: M:: ExprVec
  | e1 (+) e2                 :: M:: ExprUnion
  | ( << u : t1 >> ) -> t2    :: M:: FuncTypeVec
  | ( u : t1 ) -> t2          :: M:: DepFuncType
  | \ << u : t >> . e         :: M:: LamVec
  | e1 [ << e2 |-> t2 >> ]    :: M:: SubstVec
  | D @ << u >>               :: M:: Datatype
  | e1 [ D @ << u >> |-> e3 ] :: M:: ExprSubst
  | \\\ e                     :: M:: LineHack
  | & e                       :: M:: AlignHack
  | @@                        :: M:: DummyExpr
  | let x * : t = e2 in e1    :: M:: StarBinding
  | s                         :: M:: Sort
  | foldn [ t ] e             :: M:: CastUpMulti
  | unfoldn e                 :: M:: CastDownMulti

%% Literals
lit :: 'lit_' ::=
  | int                 :: M:: IntType
  | bool                :: M:: BoolType
  | three               :: M:: Number

%% Sorts
s {{ tex \star }}, r {{ tex \star }} :: 's_' ::= {{ com Sorts }}
  | star                ::  :: Star                  {{ com Star }}

%% Contexts
G {{ tex \Gamma }} :: 'G_' ::= {{ com Contexts }}
  | empty          ::  :: Empty       {{ com Empty }}
  | G , x : t      ::  :: Binding     {{ com Variable binding }}
  | G , G'         :: M:: CtxBind     {{ com Context binding }}
  | G [ x |-> e ]  :: M:: CtxSubst    {{ com Context substitution }}
  | G *            :: M:: CtxStar     {{ com Starred context }}
  | ee : tt         :: M:: BindCoC

%% Values
v :: 'v_' ::=                               {{ com Values   }}
  | star                 ::  :: Star       {{ com Type of types }}
  | \ x : t . e          ::  :: Lam        {{ com Abstraction  }}
  | Pi x : t1 . t2       ::  :: Pi         {{ com Product }}
  | castup [ t ] e       ::  :: CastUp     {{ com Cast up by expansion }}

%% Explicit core
ec {{ tex e }}, tc {{ tex \tau }}, Tc {{ tex \sigma }} :: 'ec_' ::=       {{ com Expressions    }}
  | x                           ::  :: Var        {{ com Variable }}
  | star                        ::  :: Star       {{ com Type of types }}
  | ec1 ec2                     ::  :: App        {{ com Application }}
  | \ x : tc . ec               ::  :: Lam        {{ com Abstraction }}
  | Pi x : tc . Tc              ::  :: Pi         {{ com Product }}
  | castup [ tc ] ec            ::  :: CastUp     {{ com Cast up by expansion}}
  | castdown ec                 ::  :: CastDown   {{ com Cast down by reduction }}

%%% Terminals
terminals :: 'terminals_' ::=
  | \        ::  :: lambda    {{ tex \lambda }}
  | Pi       ::  :: pi        {{ tex \Pi }}
  | mu       ::  :: mu        {{ tex \mu }}
  | star     ::  :: star      {{ tex \star }}
  | square   ::  :: square    {{ tex \Box }}
  | empty    ::  :: empty     {{ tex \varnothing }}
  | ->       ::  :: to        {{ tex \rightarrow }}
  | -->      ::  :: steps     {{ tex \longrightarrow }}
  | -->>     ::  :: nsteps    {{ tex \longrightarrow_n }}
  | ->>      ::  :: msteps    {{ tex \twoheadrightarrow }}
  | |->      ::  :: mapsto    {{ tex \mapsto }}
  | castup   ::  :: castup    {{ tex \kw{cast}^{\uparrow} }}
  | |-       ::  :: turnstile {{ tex \vdash }}
  | ==       ::  :: equiv     {{ tex \equiv }}
  | =a       ::  :: alphaeq   {{ tex \equiv }}
  | =b       ::  :: betaeq    {{ tex =_{\beta} }}
  | castdown ::  :: castdown  {{ tex \kw{cast}_{\downarrow} }}
  | let      ::  :: let       {{ tex \kw{let} }}
  | in       ::  :: key_in    {{ tex \kw{in} }}
  | eqb      ::  :: eqb       {{ tex =_{\beta} }}
  | int      ::  :: int       {{ tex \kw{Int} }}
  | bool     ::  :: bool      {{ tex \kw{Bool} }}
  | three    ::  :: three     {{ tex 3 }}
  | elt      ::  :: elt       {{ tex \in }}
  | *        ::  :: upstar    {{ tex ^{*} }}
  | =>       ::  :: match     {{ tex \Rightarrow }}
  | /=       ::  :: ineq      {{ tex \not \eq }}
  | data     ::  :: data      {{ tex \kw{data} }}
  | case     ::  :: case      {{ tex \kw{case} }}
  | of       ::  :: of        {{ tex \kw{of} }}
  | <<       ::  :: vecl      {{ tex \overline{ }}
  | >>       ::  :: vecr      {{ tex } }}
  | @        ::  :: marker    {{ tex }}
  | ~>       ::  :: transto   {{ tex \rightsquigarrow }}
  | (+)      ::  :: union     {{ tex \uplus }}
  | :=       ::  :: define    {{ tex \triangleq ~ }}
  | &        ::  :: align     {{ tex & }}
  | \\\      ::  :: newline   {{ tex \\ & }}
  | @@       ::  :: dummy     {{ tex }}
  | foldn    ::  :: foldn      {{ tex \kw{cast}_{\uparrow}^n }}
  | unfoldn  ::  :: unfoldn    {{ tex \kw{cast}_{\downarrow}^n }}

%%% Fomulae
formula :: 'formula_' ::=
  | judgement                          ::   :: judgement
  | G1 = G2                            ::   :: context_rewrite
  | Gs1 = Gs2                          ::   :: context_rewrite_src
  | e1 = e2                            ::   :: rewrite
  | e1 /= e2                           ::   :: inequality
  | e1 =b e2                           ::   :: beta_equality
  | e1 =a e2                           ::   :: alpha_equality
  | e1 == e2                           ::   :: equivalence
  | ( formula )                        ::   :: parens
  | x : t elt G                        ::   :: context_inclusion
  | |- e : t                           ::   :: empty_context
  | formula \\                         ::   :: lbhack {{ tex [[formula]] \ottlinebreak }}
  | u : A elt Gs                       ::   :: context_inclusion_src
  | |- E : A                           ::   :: empty_context_src
  | << formula >>                      ::   :: formula_vector
  | E1 = E2                            ::   :: rewrite_src
  | e1 := e2                           ::   :: definition
  | x fresh G                          ::   :: freshment
    {{ tex [[x]] \not \in \kw{dom}([[G]]) }}
  | ee1 =b ee2                         ::   :: beta_equality_coc

%%% Subrule
subrules
  v <:: e
  ec <:: e

%%% Typing rules
defns
LintCore :: '' ::=

defn |- G        ::  :: ctx :: 'Env_' {{ com Well-formed context }}
by

-------- :: Empty
|- empty

|- G
G |- t : s
----------- :: Var
|- G, x : t

defn G  |- e : t ::  :: expr_rec :: 'T_' {{ com Expression typing }}
by

G, x:t |- e:t
G |- t:s
------------------- :: Mu
G |- (mu x:t . e):t


defn G  |- e : t ::  :: expr :: 'T_' {{ com Expression typing }}
by

---------------------- :: Ax
empty |- star : star

|- G
x:t elt G
---------- :: Var
G |- x:t

G |- e1 : (Pi x:t2.t1)
G |- e2 : t2
------------------------ :: App
G |- e1 e2 : t1[x |-> e2]

G, x : t1 |- e : t2
G |- (Pi x:t1.t2) : s
--------------------------- :: Lam
G |- (\x:t1.e):(Pi x:t1.t2)

G |- t1 : s
G, x:t1 |- t2:r
------------------- :: Pi
G |- (Pi x:t1.t2):r

G |- e : t2
G |- t1 : s
t1 --> t2
---------------------- :: CastUp
G |- (castup[t1] e):t1

G |- e : t1
G |- t2 : s
t1 --> t2
---------------------- :: CastDown
G |- (castdown e) : t2

%%% Dynamic semantics
defns
OpSem :: '' ::=

defn e --> e' ::  :: step_rec :: 'S_' {{ com One-step reduction }}
by

------------------------------- :: Mu
mu x:t.e --> e [x |-> mu x:t.e]

defn e --> e' ::  :: step :: 'S_' {{ com One-step reduction }}
by

----------------------------  :: Beta
(\x:t.e1) e2 --> e1 [x|->e2]

---------------------------- :: CastDownUp
castdown (castup[t] e) --> e

e1 --> e1'
---------------- :: App
e1 e2 --> e1' e2

e --> e'
-------------------------- :: CastDown
castdown e --> castdown e'

